// KQL samples - Web Service Calls


// Web Service Calls (RAW data)
// Use extend if you need to use a column extracted from customDimensions in a function (such as countof)
// This example uses project-away to filter out all "internal" columns
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where operation_Name == "Web Services Call" // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| extend aadID = customDimensions.aadTenantId
, environmentType = customDimensions.environmentType
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, executionTime = customDimensions.serverExecutionTime
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
| project-rename environmentName=cloud_RoleInstance 
| project-away severityLevel, itemType, customMeasurements, client_Browser, client_City, client_CountryOrRegion, client_IP, client_OS, client_Type, client_StateOrProvince, client_Model, cloud_RoleName, operation_SyntheticSource, operation_ParentId, user_Id, user_AuthenticatedId, user_AccountId , application_Version, sdkVersion, iKey, appId, appName, itemId, itemCount, operation_Name, operation_Id


// Web Service Calls (RAW data)
// Use renames in the project operator if you don't need to filter on the columns extracted from customDimensions
// This example uses project to only include the columns needed in the result set
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Web Services Call" // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| extend aadID = customDimensions.aadTenantId
| project environmentName=cloud_RoleInstance 
, environmentType = customDimensions.environmentType
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, executionTime = customDimensions.serverExecutionTime
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, message, customDimensions